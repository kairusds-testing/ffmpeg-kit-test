plugins{
	id "com.android.application"
	id "de.undercouch.download" version "5.4.0"
}

android {
	signingConfigs {
		debug {
			storeFile file("debug.keystore")
			storePassword "android"
			keyAlias "androiddebugkey"
			keyPassword "android"
		}
	}
	namespace "com.arthenica.ffmpegkit.test"
	compileSdk 33
	defaultConfig {
		applicationId "com.arthenica.ffmpegkit.test"
		minSdk 24
		targetSdk 33
		versionCode 240510
		versionName "5.1"
	}
	buildTypes {
		debug {
			minifyEnabled false
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
			signingConfig signingConfigs.debug
		}
	}
	splits {
		abi {
			enable true
			reset()
			include "x86_64", "arm64-v8a"
			universalApk false
		}
	}
}

task downloadLib(type: Download){
	src "https://github.com/kairusds-testing/ffmpeg-kit/releases/latest/download/ffmpeg-kit.aar"
	dest new File(buildDir.parent, "ffmpeg-kit.aar")
	overwrite false
}

preBuild.dependsOn downloadLib

ext.abiCodes = ["x86_64": 2, "arm64-v8a": 4]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
	variant.outputs.each {
		output ->
			def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
			output.versionCodeOverride = baseAbiVersionCode + variant.versionCode
	}
}

dependencies {
	implementation files("ffmpeg-kit.aar")
	implementation "com.arthenica:smart-exception-java:0.2.1"
	implementation "androidx.appcompat:appcompat:1.6.1"
	implementation "androidx.constraintlayout:constraintlayout:2.1.4"
}
